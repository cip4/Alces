import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'org.springframework.boot' version '2.7.0'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'groovy'
    id 'application'
    id 'org.beryx.runtime' version '1.13.0'
    id 'jacoco'
}

def simpleProjectName = "Alces"
mainClassName = 'org.cip4.tools.alces.Application'
group = 'org.cip4.tools.alces'
description = 'CIP4 ' + simpleProjectName
version = project.hasProperty('projectVersion') ? project.projectVersion : 'development'

java {
    archivesBaseName = simpleProjectName
    withJavadocJar()
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

processResources {
    filesMatching(['**/*.properties', '**/*.yml']) {
        filter(ReplaceTokens, tokens: [
                'name': project.properties['name'],
                'version': project.properties['version'],
                'artifactId': project.properties['name'],
                'timestamp': new Date().format('yyyy-MM-dd HH:mm:ss')
        ])
    }
}

application {
    mainClass = 'org.cip4.tools.alces.Application'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.codehaus.groovy:groovy:+'
    implementation 'org.codehaus.groovy:groovy-xml:+'
    implementation 'org.cip4.lib.jdf:JDFLibJ:2.1.7.+'
    implementation 'com.formdev:flatlaf:3.+'

    implementation 'org.xhtmlrenderer:core-renderer:R8'

    testImplementation('org.mockito:mockito-core')
    testImplementation('org.mockito:mockito-junit-jupiter')
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

runtime {
    jpackage {
        modules = [
                'java.desktop',
                'java.xml',
                'java.sql',
                'java.compiler',
                'java.logging',
                'java.prefs',
                'java.management',
                'jdk.xml.dom',
                'java.datatransfer',
                'java.naming',
                'java.security.jgss',
                'java.instrument',
                'jdk.crypto.ec',
                'jdk.unsupported'
        ]
        imageOptions = [
                "--copyright", "CIP4 Organization",
                "--vendor", "CIP4 Organization"
        ]
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def icon = "src/main/dist/alces.ico"
        if (currentOs.macOsX) {
            imageOptions += [
                    "--mac-sign",
                    "--mac-signing-key-user-name", "CIP4 Organization (4DL5XX9SG8)",
                    "--mac-package-identifier", "org.cip4.alces"
            ]
            icon = "src/main/dist/alces.icns"
        }
        if (currentOs.windows) {
            installerOptions += [
                    "--win-dir-chooser",
                    "--win-menu",
                    "--win-upgrade-uuid", "12abef0e-2d7f-4c51-9c8b-72ecb743bddc",
                    "--vendor", "CIP4 Organization",
            ]
        }
        imageOptions += ["--icon", icon]
        appVersion = project.version == "development" ? "1.0.0" : project.version
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = false
    }
}
test {
    useJUnitPlatform()
    dependsOn(jre)
    if (GradleVersion.current() >= GradleVersion.version("8.0")) {
        executable = jre.getJreDir().dir("bin").file("java").getAsFile().getAbsolutePath()
    }
}


jar {
    manifest {
        attributes(
                "Implementation-Title": project.description,
                "Implementation-Version": project.version,
                "Implementation-Vendor-Id": project.group,
                "Specification-Title": project.description,
                "Specification-Version": project.version,
                "Build-Jdk": JavaVersion.current(),
        )
    }
}

startScripts {
    applicationName = simpleProjectName
}

tasks.withType(Test) {
    testLogging {
        events TestLogEvent.FAILED,
               TestLogEvent.SKIPPED
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true
    }
}