import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'application'
    id 'distribution'
    id 'edu.sc.seis.launch4j' version '2.4.9'
    id 'edu.sc.seis.macAppBundle' version '2.3.0'
}

if (project.hasProperty('projectVersion')) {
    version = project.projectVersion
} else {
    version = 'development'
}

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

processResources {
    filesMatching(['**/*.properties', '**/*.yml']) {
        filter(ReplaceTokens, tokens:  [
                'name': project.properties['name'],
                'version': project.properties['version'],
                'timestamp': new Date().format('yyyy-MM-dd HH:mm:ss')
        ])
    }
}

application {
    mainClass = 'org.cip4.tools.alces.swingui.Alces'
}

launch4j {
    icon = "${projectDir}/src/main/dist/alces.ico"
    outfile = project.name + '-' + project.version + '.exe'
    headerType = 'gui'
}

macAppBundle {
    mainClassName = 'org.cip4.tools.alces.swingui.Alces'
    icon = "${projectDir}/src/main/dist/alces.icns"
    javaProperties.put("apple.laf.useScreenMenuBar", "true")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.cip4.lib.jdf:JDFLibJ:2.1.7.+'
    implementation 'jdom:jdom:1.1'
    implementation 'org.xhtmlrenderer:core-renderer:R8pre2'
    implementation 'concurrent:concurrent:1.3.4'
    implementation 'commons-vfs:commons-vfs:1.0'
    implementation 'org.mozilla:rhino:1.7R4'
    implementation 'commons-httpclient:commons-httpclient:3.1'
    // implementation 'jetty:jetty:5.1.10'
    // implementation 'jetty:org.mortbay.jmx:5.1.10'
    implementation 'jaxen:jaxen:1.1.4'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
